import com.organization.weathermap.buildsrc.Libs

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: "$project.rootDir/jacoco.gradle"

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.organization.weathermap"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.organization.weathermap.HiltTestRunner"
        buildConfigField("String", "API_KEY", apikeyProperties['API_KEY'])
    }
    // to give  unit tests to all the resources, assets and manifests.
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Compose
    implementation Libs.Compose.ui
    implementation Libs.Compose.material
    implementation Libs.Compose.uiToolingPreview
    debugImplementation Libs.Compose.uiTooling
    debugImplementation Libs.Compose.uiTestManifest
    implementation Libs.Navigation.compose
    implementation Libs.Compose.activity
    implementation Libs.Compose.viewModel
    implementation Libs.Compose.coil

    // Testing
    androidTestImplementation Libs.Compose.uiTestJUnit4
    androidTestImplementation Libs.Testing.uiAutomator
    androidTestImplementation Libs.Testing.rules
    androidTestImplementation Libs.Testing.mockWebServer
    androidTestImplementation  Libs.Testing.coreKtx
    debugImplementation   Libs.Testing.coreKtx

    testImplementation Libs.Compose.coroutinesTest
    testImplementation Libs.Testing.jUnit
    testImplementation Libs.Testing.junitKtx
    testImplementation Libs.Testing.mockito
    testImplementation Libs.Testing.mockWebServer


    // Hilt
    implementation Libs.Hilt.android
    kapt Libs.Hilt.androidCompiler
    kapt Libs.Hilt.compiler
    // For instrumented tests.
    androidTestImplementation Libs.Hilt.androidTesting
    // ...with Kotlin.
    kaptAndroidTest Libs.Hilt.androidCompiler
    // For Robolectric tests.
    testImplementation Libs.Hilt.androidTesting
    // ...with Kotlin.
    kaptTest Libs.Hilt.androidCompiler
    // compose navigation
    implementation Libs.Hilt.navigation


    // NETWORK
    implementation Libs.Network.gson
    implementation Libs.Network.gsonConverter
    implementation Libs.Network.retrofit
    implementation Libs.Network.okhttp
    implementation Libs.Network.loggingInterceptor

    // Cache
    implementation Libs.Cache.runtime
    implementation Libs.Cache.ktx
    kapt Libs.Cache.compiler
    // Utils
    implementation Libs.Others.timber
}